Program Mandelbrot

	Var ColorMap2		As Int[100]
	Var ColorSequence	As Int[14] 	= { Violet, DarkViolet, Indigo, DarkBlue, Blue, DarkCyan, Green, GreenYellow, Yellow, Goldenrod, Orange, DarkOrange, Red, Magenta }
	
	Var backgroundColor	As Int		= White
	
	// The globals that define the actual plotting region.
	// Let's keep the region sqare, please.
	Var windowSize		As Int = 400
	Var xmin 			As Decimal 	= -2.1
	Var ymin 			As Decimal 	= -1.35
	Var xmax 			As Decimal 	=  0.6
	Var ymax 			As Decimal 	=  1.35
	
	// Globals used by the asynchronous button event routine.
	Var zoomingIn		As Bool		= False
	Var newCoords		As Bool		= False
	Var ButtonDown		As Bool		= False
	Var xPos 			As Decimal
	Var yPos 			As Decimal
	Var xSpan 			As Decimal
	Var ySpan 			As Decimal
	Var xCenter			As Int
	Var yCenter			As Int
	Var halfSquareSize	As Int
			
	// Number of iteration before a point is considered to be in the set.
	Var threshold		As Int		= 100

	//
	// SetColorMap -- set up the colors to be plotted
	//
	Method SetColorMap()
	
		// This needs to be better.  I want a smooth transition through the colors of the rainbow,
		// perhaps several up to the threshold limit.
		Var index As Int
		
		For index = 1 To ArrayLength(ColorMap2)
			ColorMap2[index] = ColorSequence[index Mod ArrayLength(ColorSequence) + 1]
		Next
		
	End Method	

	//
	// PlotMandelbrot -- Calculate and plot the portion of the set corresponding to the arguments.
	//
	Function PlotMandelbrot(sizeX As Int, sizeY As Int, minX As Decimal, minY As Decimal, maxX As Decimal, maxY As Decimal, threshold As Int) As Bool

		Var x 			As Decimal
		Var y 			As Decimal
		Var x1 			As Decimal
		Var y1 			As Decimal
		Var xx 			As Decimal
		
		Var looper 		As Int
		Var s 			As Int 
		Var z 			As Int
		
		Var integralX 	As Decimal = (maxX - minX) / sizeX
		Var integralY 	As Decimal = (maxY - minY) / sizeY
		Var xSquared	As Decimal
		Var ySquared	As Decimal
		
		ClearBackground( Colors.White )
		
		x = minX
		For s = 1 To sizeX - 1
		
			y = minY
			
			For z = 1 To sizeY - 1
			
				x1 = 0
				y1 = 0
				xSquared = 0
				ySquared = 0
				looper = 0

				// Check to see if the magnitude of the point has gone over two.
				While looper < threshold And ( xSquared + ySquared ) < 4.0
				
					looper = looper + 1
					xx = xSquared - ySquared + x
					y1 = 2 * x1 * y1 + y
					x1 = xx
					xSquared = x1 * x1
					ySquared = y1 * y1
					
				End While
					
				// Only call putpixel if we need to plot a point not in the set.
				If looper <> threshold Then
					DrawPoint( s, z, ColorMap2[looper] )
				End If
				
				y = y + integralY
			
			Next
			
			x = x + integralX
			
			// Check for any key press to interrupt.
			If GetKey() <> ""Then
				Return False
			End If
			
		Next
		
		RefreshScreen()
		
		Return True
		
	End Function
	
	
	//
	// GetNewCoordinates -- Allow the user to use the mouse and/or keyboard to get the dims of the next plot.
	//
	Method GetNewCoordinates ()
		
		While Mouse.Event <> ""
			DoEvents()
		End While
		
		// Reset the newCoords flag before setting up the mouse events.
		newCoords = False
		ButtonDown = False
		ZoomingIn = False
		
		CopyRectangle( 0, 0, ScreenWidth(), ScreenHeight(), ScreenWidth() + 1, 0 )
		
		While newCoords = False And Not IsKeyDown( Escape )
			
			HandleMouse()
			Delay( 1 )
			
		End While
		
	End Method

	//
	// OnMouseEvent -- The user interacts with the program to zoom in or out for the next plot.
	//
	Method HandleMouse()

		Var delta As Decimal
		
		If Not ButtonDown And (LeftPressed Or RightPressed) Then
		
			// Here's the point where the user clicked: the new center.
			xCenter = mouseX
			yCenter = mouseY
			ButtonDown = True
			
			If LeftPressed Then
			
				halfSquareSize = 0
				zoomingIn = True
				
				Return
				
			End If

			If RightPressed Then
			
				// User is zooming out.  Calculate the new coordinates.
				xSpan = xmax - xmin		
				ySpan = ymax - ymin
				xPos = xCenter / ConvertToDecimal(ScreenWidth() ) * xSpan + xmin
				yPos = yCenter / ConvertToDecimal(ScreenHeight()) * ySpan + ymin
				xmin = xPos - xSpan
				xmax = xPos + xSpan
				ymin = yPos - ySpan
				ymax = yPos + ySpan
				
				newCoords = True
				Return
				
			End If
			
			Return
			
		End If
		
		If ButtonDown And (Not LeftPressed) And (Not RightPressed) Then
		
			xSpan = xmax - xmin		
			ySpan = ymax - ymin
			xPos = xCenter / ConvertToDecimal(ScreenWidth() ) * xSpan + xmin
			yPos = yCenter / ConvertToDecimal(ScreenHeight()) * ySpan + ymin
			
			// We're done.  Convert the square into coords.
			// If the user clicked clicked without drawing a square, then zoom in with factor of ten.
			If halfSquareSize = 0 Then
				halfSquareSize = ScreenWidth()/20
			End If
			
			delta = xSpan * ConvertToDecimal(halfSquareSize) / ConvertToDecimal(ScreenWidth())

			xmin = xPos - delta
			xmax = xPos + delta
			ymin = yPos - delta
			ymax = yPos + delta
			
			zoomingIn = False
			newCoords = True
			Return
			
		End If
		
		If ButtonDown And zoomingIn = True Then
			
			// Translate the distance from the center into the HalfSquareSize and paint.
			halfSquareSize = Abs(mouseX - xCenter)
			
			// Turn off the refresh until we're done painting.
			BeginFrame()
			
			CopyRectangle( ScreenWidth() + 1, 0, ScreenWidth(), ScreenHeight(), 0, 0 )

			Pen(False)
			MoveTo(xCenter - halfSquareSize, yCenter - halfSquareSize)
			Pen(True)
			PenWidth(1)
			Color(Black)
			Rectangle(halfSquareSize * 2, halfSquareSize * 2, False)
		
			// Paint screen.
			RefreshScreen()
			
		End If
			
	End Method 
	
	Method Main()
	
		SetScreenSize( windowSize, windowSize )
		
		Var StartTime As Decimal
		
		While Not IsKeyDown( Escape )
		
			ClearBackground(backgroundColor)
			SetColorMap()
			
			Status ("Plotting (" + RoundToPlace(xmin, 7) + "," + RoundToPlace(ymin, 7) + ") to (" + RoundToPlace(xmax, 7) + "," + RoundToPlace(ymax, 7) + ").  Hit any key to abort.")
			
			StartTime = TickCount()
			If PlotMandelbrot(ScreenWidth(), ScreenHeight(), xmin, ymin, xmax, ymax, threshold) = True Then
				Status (RoundToPlace( (TickCount() - StartTime) /1000, 2) + " seconds.  Left click to zoom in; right click to zoom out")
			Else
				Status ("Aborted.  Left click to zoom in; right click to zoom out")
			End If
			
			GetNewCoordinates ()
			
		End While
		
	End Method 
	
End Program 
