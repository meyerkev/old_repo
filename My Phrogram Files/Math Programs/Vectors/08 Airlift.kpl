Program Airlift

	// A vector game.  
	//
	// Jamey Friend, June 7, 2006
	// 278 lines of code
	// 
	// Airlift is not a simple vector addition problem like TheRiverboat.
	// For a hint, see the comment at the end of the program.
		
	
	Method Main()
		Define WindSpeed As RealVector              // Velocities of the
		Define SpeedOfPlane As RealVector           // wind and the plane
		
		Define Angle As Decimal                     // Random angle of plane
		
		Define V As RealVector                      // The user's choice
		Define Path As RealVector                   // Path of plane
		
		Define Jungle As Sprite                     // Background image
		
		Define Plane As Sprite
		Define Parachute As Sprite
		Define Village As Sprite
		
		Define W As Integer = 800                   // Dimensions of screen is set
		Define H As Integer = 520                   // by size of background image
		
		Define Head As Point                        // Two points choen by user
		Define Tail As Point
		
		Define GotHead As Boolean                   // True if user clicks a point
		Define GotTail As Boolean                   // True if user clicks another point

		Define HitTarget As Boolean                 // True if plane goes over village
		Define ParachuteDropped As Boolean          // True when chute is dropped
		
		Define x As Decimal                         // Amount the plane moves
		Define y As Decimal
		
		
		// Set up the screen
		SetScreenSize ( W, H )
		SetFont ( Arial, 11, True, False, False )
		Color ( White )
		PenWidth ( 3 )
		
		// Load the sprites.
		Village.Load ( "Village.gif" )
		Village.MoveTo ( W - 200, 200 ) 
		Jungle.Load ( "Jungle.jpg" )
		Parachute.Load ( "Parachute.gif" )
		Parachute.Scale ( 80, 80 )
			
		While True
			// Show the jungle and the village.
			Jungle.Stamp ( )
			Village.Show ( )
			
			// Load and show the plane.
			Plane.Load ( "Plane.gif" )
			Plane.Scale ( 50, 50 )
			Plane.MoveTo ( 60, H - 100 )
			Angle = Random ( -50, 10 )
			Plane.Rotation = Angle
			Plane.Show ( )

			// Be sure the parachute is below the plane.
			Plane.ZIndex = 10
			Parachute.ZIndex = 5
			
			// Show the wind velocity.
			WindSpeed.Create ( Random ( 0, 360 ), RandomDecimal ( 20, 50 ))
			WindSpeed.Position.X = 550
			WindSpeed.Position.Y = 300 
			WindSpeed.Draw ( )
			WindSpeed.Label ( "Velocity of wind" )
		
			// Show the velocity of the plane.
			SpeedOfPlane.Create ( Angle, RandomDecimal( 2.5 * WindSpeed.Length, 4*WindSpeed.Length )) 
			SpeedOfPlane.Position = CreatePoint ( 450 , 400 ) 
			SpeedOfPlane.Draw ( )
			SpeedOfPlane.Label ( " Airspeed and heading of plane" )
			
			// Give instructions.
			JumpTo ( 50, 20 )
			PrintLine ( "The village of Ismali has been surrounded by enemy troops" )
			PrintLine ( "for weeks and the villagers are starving." )
			PrintLine ( "" )
			PrintLine ( "Your task is to airlift food and medicine to the village." )
			PrintLine ( "Click two points to show the direction you want to steer the plane." )
		
			While True	
				
				// Wait for the user to click two points.
				If MouseEvent = "ButtonDown" Then
					If  Not GotTail Then
						Tail =( CreatePoint ( MouseX, MouseY ))
						GotTail = True
					ElseIf Not GotHead Then
						Head = ( CreatePoint ( MouseX, MouseY ))
						GotHead = True
					End If
					While MouseEvent = "ButtonDown"
						Delay ( 1 )
					End While
				End If
			
				// When the user has clicked two points, let the plane fly.
				If GotHead And GotTail Then
					// Get the messages and vectors out of the way.
					ClearBackground ( White )
					Jungle.Stamp ( )
					Plane.Show ( )
					
					// Create the direction vector from the two points 
					// the user clicked.
					V.CreateFromPoint ( CreatePoint ( Head.X - Tail.X, Head.Y - Tail.Y ))
					
					// Change the orientation of the plane.
					SpeedOfPlane.Direction = V.Direction
					Plane.Rotation = V.Direction
					Plane.Show ( )
					
					// Calculate the path the plane will take.
					Path = SpeedOfPlane.Add ( WindSpeed )
					Path.Position = Plane.GetCenter ( )
					
					// Let the plane travel a bit at a time (until it goes off screen).
					// See if it flies over the village.
					While Plane.Top < H And Plane.Top + Plane.Height > 0 And Plane.Left + Plane.Width > 0 And Plane.Left < W + Plane.Width
						x = Cos ( DegreesToRadians ( Path.Direction ))
						y = Sin ( DegreesToRadians ( Path.Direction ))
						Plane.MoveByAmount ( x, y )
					
						If ParachuteDropped Then
							Parachute.MoveToward ( Village.GetCenter ( ), 1 )
						End If
						
						// If the plane is over the village, drop the parachute.
						
						If Plane.Intersects ( Village ) Then
						 	HitTarget = True
							Parachute.Show ( )
							If Not ParachuteDropped Then
								Parachute.MoveTo ( Plane.Left + 30, Plane.Top + 30 )
							End If
							ParachuteDropped = True
						End If
						
						Delay ( 5 )
					End While
					
					// Let the user know the results.
					JumpTo ( 150, 50 )
					SetFont ( Arial, 14, True, False, False )
					If HitTarget Then
						PrintLine ( "You have saved the villagers!" )
						PrintLine ( "You are a true humanitarian!" )
						PrintLine ( "" )
					Else
						PrintLine ( "You missed the village." )
						PrintLine ( "Don't give up.  Try again." )
					End If
					PrintLine ( "" )
					SetFont ( Arial, 11, True, False, False )
					Break
				End If 
			
				RefreshScreen ( )
			End While	
		
			// Ask the user to click anywhere to continue.
			ContinueToNext ( )
			
			// After the user clicks somewhere, prepare to go around again.
			//ClearBackground ( White )
			GotHead = False
			GotTail = False
			HitTarget = False
			Parachute.Hide ( )
			ParachuteDropped = False
			
		End While
		
	End Method

	Class RealVector 
		// This class defines a vector space over the real numbers.
		// Each vector is defined by a direction and a length.
		// The standard representation of the vector is with its
		// tail at the point (0, 0) and its head at a point
		// determined by the vector's direction and length.  The 
		// representation may be moved by changing the value of
		// Position (the location of the tail).

		Define Direction As Decimal
		Define Length As Decimal
		
		Define Position As Point
		
		Function ToString ( ) As String
			// Returns a description of the vector.
			Return "Direction = " + RoundToPlace ( Direction, 4 ) + " degrees, Length = " + RoundToPlace ( Length, 4 )
		End Function
		
		Method Label ( S As String )
			// Labels a representation of the vector.
			Define x As Decimal = LinearInterpolation ( Position.X, Head ( ).X, .5 )
			Define y As Decimal = LinearInterpolation ( Position.Y, Head ( ).Y, .5 )
			JumpTo ( x, y )
			Print ( S )
		End Method
		
		Method Draw ( )
			// Draws a representation of the vector.
			Define VX As RealVector 
			
			DrawLine ( Head ( ).X, Head ( ).Y, Position.X, Position.Y )
			
			// Draw arrowhead.
			VX.Create ( Direction, 10 )
			VX.Negate ( )
			VX.Position =  Head ( )

			VX.Rotate ( 20 )
			DrawLine ( VX.Head ( ).X, VX.Head ( ).Y, VX.Position.X, VX.Position.Y )
		
			VX.Rotate ( -40 )
			DrawLine ( VX.Head ( ).X, VX.Head ( ).Y, VX.Position.X, VX.Position.Y )
		
		End Method
		
		Method Negate ( )
			// Negates a vector, i.e., changes its direction by 180 degrees.
			Direction = CanonicalAngle ( Direction + 180 )
		End Method
		
		Function Normalize ( ) As RealVector
			// Returns a unit vector.
			Return CreateRealVector ( Direction, 1.0 )
		End Function
		
		Function Equals ( V As RealVector ) As Boolean
			// Determines is this vector is equal to the vector V.
			Return RoundToPlace ( V.Direction, 3 ) = RoundToPlace ( Direction, 3 ) And RoundToPlace ( V.Length, 3 ) = RoundToPlace ( Length, 3 )
		End Function
		
		Function MultiplyByScalar ( X As Decimal ) As RealVector
			// Multiplies a vector by a scalar.
			Return CreateRealVector ( Direction, X*Length )
		End Function
		
		Method Rotate ( A As Decimal )
			// Rotates this vector by the angle A.
			Direction = CanonicalAngle ( Direction + A )
		End Method
		
		Function Add ( V As RealVector ) As RealVector
			// Adds V to this vector and returns the sum
			// as a vector in the standard position - with
			// the position (0, 0).
			Define VX As RealVector
			Define D As Decimal
			
			VX = V.Clone ( ) //Create ( V.Direction, V.Length )
			VX.Position = Head ( )
			
			Define x As Decimal = VX.Head ( ).X - Position.X
			Define y As Decimal = VX.Head ( ).Y - Position.Y
			
			If x = 0 Then
				If y >= 0 Then
					D = 90
				Else
					D = -90
				End If
			Else
				D = CanonicalAngle ( RadiansToDegrees ( ArcTan ( y/x )))
			End If
			
			Define L As Decimal = Sqrt ( x*x + y*y )
			
			If x < 0 Then 
				D = CanonicalAngle ( D + 180 )
			End If
			
			Return CreateRealVector ( D, L )
		End Function
	
		Function Subtract ( V As RealVector ) As RealVector
			// Subtracts V from this vector.
			Define VX As RealVector = V
			VX.Negate ()
			VX = Add ( VX )
			Return VX
		End Function
		
		Function DotProduct ( V As RealVector ) As Decimal
			// Calculates the dot product of this vector and
			// the vector V.
			Define a As Decimal = Head ( ).X
			Define b As Decimal = Head ( ).Y
			
			Define x As Decimal = V.Head ( ).X
			Define y As Decimal = V.Head ( ).Y
			
			Return a*x + b*y
		End Function
		
		Method Create ( D As Decimal, L As Decimal )
			// Creates a vector with a direction D and a length L
			If L < 0 Then
				L = Abs ( L )
				D = CanonicalAngle ( D + 180 )
			End If
			Direction = CanonicalAngle ( D )
			Length = L
			Position = CreatePoint ( 0, 0 )
		End Method
		
		Function CreateRealVector ( D As Decimal, L As Decimal ) As RealVector
			// Returns a new RealVector with direction D and length L
			Define V As RealVector
			If L < 0 Then
				L = Abs ( L )
				D = D + 180
			End If
			V.Direction = CanonicalAngle (D)
			V.Length = L
			V.Position = CreatePoint ( 0, 0 )
			Return V
		End Function

		Method CreateFromPoint ( P As Point )
			// Creates a vector with the tail at (0, 0) and the
			// head at the point P.
			Head ( ) = P
			If P.x = 0 Then
				If P.y >= 0 Then
					Direction = 90
				Else
					Direction = 270
				End If
			Else
				Direction = CanonicalAngle ( RadiansToDegrees ( ArcTan ( P.y/P.x )))
				If P.X < 0 Then
					Direction = CanonicalAngle ( Direction - 180 )
				End If
			End If
			Length = Sqrt ( P.X*P.X + P.Y*P.Y )
			Position = CreatePoint ( 0, 0 )
		End Method
		
		Function Clone (  ) As RealVector
			// Returns a copy of this vector.
			Return CreateRealVector ( Direction, Length )
		End Function
		
		Function CanonicalAngle ( A As Decimal ) As Decimal
			// Reduces an angle in degrees to its canonical form, i.e.,
			// to a value from 0 up to (but not including) 360.
			While A < 0
				A = A + 360
			End While
		
			While A >= 360
				A = A - 360
			End While
		
			Return A
		End Function
		
		Function ComponentX ( ) As RealVector
			// Returns a component vector in the x-direction.
			// The component is in the same position as this vector.
			Define V As RealVector
			V.Create ( 0, Head ( ).X - Position.X )
			V.Position = Position
			Return V
		End Function
		
		Function ComponentY ( ) As RealVector
			// Returns a component vector in the y-direction.
			// The component is in the same position as this vector.
			Define V As RealVector
			V.Create ( 90, Head ( ).Y - Position.Y )
			V.Position = Position
			Return V
		End Function
		
		Method JumpTo ( x As Integer , y As Integer )
			// This is a trick to move invisibly to the point (x, y).
			DrawLine ( x, y, x, y )
		End Method
	
		Function Head ( ) As Point
			// Determines where the head of this vector is.
			Return CreatePoint ( Length*cos ( DegreesToRadians ( Direction )) + Position.X, Length*Sin ( DegreesToRadians ( Direction ))+ Position.Y)
		End Function
		
	End Class
	
	Method JumpTo ( x As Integer , y As Integer )
		// This is a trick to move invisibly to the point (x, y).
		DrawLine ( x, y, x, y )
	End Method
	
	Method JumpToPoint ( P As Point )
		// This is a trick to move invisibly to the point P.
		DrawLine ( P.X, P.Y, P.X, P.Y )
	End Method
	
	Method ContinueToNext ( )
		// Ask the user to click to continue.  Wait for the click.
		
		Print ( "Want to try again?  Click anywhere." )
		
		While Not MouseEvent = "ButtonDown"
			Delay ( 1 )
		End While
		
		While MouseEvent = "ButtonDown"
			Delay ( 1 )
		End While

	End Method

	Method Hint ()
			
	// Let the vector VP represent the velocity of the the plane.
	// Let the vector VW represent the velocity of the wind.
	//
	// Solve the problem in two steps:
	//
	// (1) Correct the course. (Rotate VP so its direction is
	//     the same as the direction from the plane to the village.)
	// (2) Correct for the wind. (Add VP to the negative of VW.)
	End Method

	End Program
