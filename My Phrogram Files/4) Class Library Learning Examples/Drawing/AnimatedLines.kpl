Program AnimatedLines

	// In this program lines are drawn slowly enough so you can watch
	// move from start to finish.
	
	// Jamey Friend, Feb. 9, 2006
	
	// Change the value of LineWidth to make the lines wider or narrower.
	Define LineWidth As Int = 4
	
	// Change the the value of Pause to draw the lines faster or slower.
	Define Pause As Int = 1
		
	Method Main()
	
		// Put your own DrawSlowLine statements in place of the following.
	
		DrawSlowLine ( 100, 100, 400, 100 )
		DrawSlowLine ( 400, 100, 400, 400 )
		DrawSlowLine ( 400, 400, 100, 400 )
		DrawSlowLine ( 100, 400, 100, 100 )
		
		DrawSlowLine ( 100, 250, 150, 400 )
		DrawSlowLine ( 150, 400, 250, 100 )
		DrawSlowLine ( 250, 100, 350, 400 )
		DrawSlowLine ( 350, 400, 400, 250 )
		
		DrawSlowLine ( 400, 250, 350, 100 )
		DrawSlowLine ( 350, 100, 250, 400 )
		DrawSlowLine ( 250, 400, 150, 100 )
		DrawSlowLine ( 150, 100, 100, 250 )

	End Method

	Method DrawSlowLine ( x1 As Decimal, y1 As Decimal, x2 As Decimal, y2 As Decimal )
		Define Slope As Decimal
		Define x As Integer
		Define y As Integer
		
		
		// Special case: Horizontal line
		If y1 = y2 Then
			If x1 < x2 Then
				For x = x1 To x2 
					DrawLine ( x, y1, x, y1 )
					Rectangle ( LineWidth, LineWidth, True )
					Delay ( Pause )
				Next
			Else
				For x = x2 To x1
					DrawLine ( x2 + x1 - x, y1, x2 + x1 - x, y1 )
					Rectangle ( LineWidth, LineWidth, True )
					Delay ( Pause )
				Next
			End If
			Return
		End If
		
			
		Slope  = ( y2 - y1 )/( x2 - x1 )


		// Special case: Slope is small	
		If Abs ( Slope ) < 1 Then
			If x1 < x2 Then
				For x = x1 To x2 
					y = Slope*( x - x1 ) + y1
					DrawLine ( x, y, x, y )
					Rectangle ( LineWidth, LineWidth, True )
					Delay ( Pause )
				Next
			Else
				For x = x2 To x1
					y =  Slope*( x1 - x ) + y2
					DrawLine ( x1 + x2 - x, y,  x1 + x2 - x, y )
					Rectangle ( LineWidth, LineWidth, True )
					Delay ( Pause )
				Next
			End If
			Return
		End If
		
		
		// Special case: Slope is steep
		If y1 < y2 Then
			For y = y1 To y2
				x = ( y - y1 )/Slope + x1
				DrawLine ( x, y, x, y )
				Rectangle ( LineWidth, LineWidth, True )
				Delay ( Pause )
			Next
		Else
			For y = y2 To y1
				x = ( y1 - y )/Slope + x2
				DrawLine ( x, y1 + y2 - y, x, y1 + y2 - y )
				Rectangle ( LineWidth, LineWidth, True )
				Delay ( Pause )
			Next
		End If

	End Method
	
End Program