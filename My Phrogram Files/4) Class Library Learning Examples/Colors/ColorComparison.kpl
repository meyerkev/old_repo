Program ColorComparison

	// Compare colors by dragging color swatches around the screen
	//
	// Jamey Friend, October 10, 2006, Revised February 19, 2007
	// Requires Phrogram
	// 167 lines of code
	
	
	// Define global variables
	Define Swatch As Sprite [140]              // The color swatches and
	Define SwatchWidth As Integer = 120        // their size.
	Define SwatchHeight As Integer = 22
	
	Define Active As Boolean [140]             // The active color swatches (sprites)
	Define z As Integer                        // Current max value of the z-index
	Define Dragging As Boolean                 // Are we dragging a swatch?
		
	Define ColorNumber As Integer              // Number of the current color
		
		
	Method Main()
		
		// Define constants.
		Define ColorName As String [140] = Split ("AliceBlue,AntiqueWhite,Aqua,Aquamarine,Azure,Beige,Bisque,Black,BlanchedAlmond,Blue,BlueViolet,Brown,BurlyWood,CadetBlue,Chartreuse,Chocolate,Coral,CornflowerBlue,Cornsilk,Crimson,Cyan,DarkBlue,DarkCyan,DarkGoldenrod,DarkGray,DarkGreen,DarkKhaki,DarkMagenta,DarkOliveGreen,DarkOrange,DarkOrchid,DarkRed,DarkSalmon,DarkSeaGreen,DarkSlateBlue,DarkSlateGray,DarkTurquoise,DarkViolet,DeepPink,DeepSkyBlue,DimGray,DodgerBlue,FireBrick,FloralWhite,ForestGreen,Fuchsia,Gainsboro,GhostWhite,Gold,Goldenrod,Gray,Green,GreenYellow,Honeydew,HotPink,IndianRed,Indigo,Ivory,Khaki,Lavender,LavenderBlush,LawnGreen,LemonChiffon,LightBlue,LightCoral,LightCyan,LightGoldenrodYellow,LightGreen,LightGray,LightPink,LightSalmon,LightSeaGreen,LightSkyBlue,LightSlateGray,LightSteelBlue,LightYellow,Lime,LimeGreen,Linen,Magenta,Maroon,MediumAquamarine,MediumBlue,MediumOrchid,MediumPurple,MediumSeaGreen,MediumSlateBlue,MediumSpringGreen,MediumTurquoise,MediumVioletRed,MidnightBlue,MintCream,MistyRose,Moccasin,NavajoWhite,Navy,OldLace,Olive,OliveDrab,Orange,OrangeRed,Orchid,PaleGoldenrod,PaleGreen,PaleTurquoise,PaleVioletRed,PapayaWhip,PeachPuff,Peru,Pink,Plum,PowderBlue,Purple,Red,RosyBrown,RoyalBlue,SaddleBrown,Salmon,SandyBrown,SeaGreen,Seashell,Sienna,Silver,SkyBlue,SlateBlue,SlateGray,Snow,SpringGreen,SteelBlue,Tan,Teal,Thistle,Tomato,Turquoise,Violet,Wheat,White,WhiteSmoke,Yellow,YellowGreen", ",")
		Define RGB As String [140] = split ("240,248,255;250,235,215;0,255,255;127,255,212;240,255,255;245,245,220;255,228,196;0,0,0;255,235,205;0,0,255;138,43,226;165,42,42;222,184,135;95,158,160;127,255,0;210,105,30;255,127,80;100,149,237;255,248,220;220,20,60;0,255,255;0,0,139;0,139,139;184,134,11;169,169,169;0,100,0;189,183,107;139,0,139;85,107,47;255,140,0;153,50,204;139,0,0;233,150,122;143,188,143;72,61,139;47,79,79;0,206,209;148,0,211;255,20,147;0,191,255;105,105,105;30,144,255;178,34,34;255,250,240;34,139,34;255,0,255;220,220,220;248,248,255;255,215,0;218,165,32;128,128,128;0,128,0;173,255,47;240,255,240;255,105,180;205,92,92;75,0,130;255,255,240;240,230,140;230,230,250;255,240,245;124,252,0;255,250,205;173,216,230;240,128,128;224,255,255;250,250,210;144,238,144;211,211,211;255,182,193;255,160,122;32,178,170;135,206,250;119,136,153;176,196,222;255,255,224;0,255,0;50,205,50;250,240,230;255,0,255;128,0,0;102,205,170;0,0,205;186,85,211;147,112,219;60,179,113;123,104,238;0,250,154;72,209,204;199,21,133;25,25,112;245,255,250;255,228,225;255,228,181;255,222,173;0,0,128;253,245,230;128,128,0;107,142,35;255,165,0;255,69,0;218,112,214;238,232,170;152,251,152;175,238,238;219,112,147;255,239,213;255,218,185;205,133,63;255,192,203;221,160,221;176,224,230;128,0,128;255,0,0;188,143,143;65,105,225;139,69,19;250,128,114;244,164,96;46,139,87;255,245,238;160,82,45;192,192,192;135,206,235;106,90,205;112,128,144;255,250,250;0,255,127;70,130,180;210,180,140;0,128,128;216,191,216;255,99,71;64,224,208;238,130,238;245,222,179;255,255,255;245,245,245;255,255,0;154,205,50", ";")	
		Define NumberOfColumns As Integer = 7
		Define NumberOfRows As Integer = 20

		// Define variables.
		Define OffsetX As Integer              // Distance from the mouse to 
		Define OffsetY As Integer              // upper left corner of swatch
		
		Define Row As Integer                  // Position of color swatch
		Define Column As Integer
		
		Define x As Integer                    // x- and y-coordinates 
		Define y As Integer
		
		
		Maximize ( )
		SwatchWidth = ScreenWidth ( )/7
		If ScreenWidth ( ) < 800 Then
			SwatchWidth = SwatchWidth + 5
		End If
		SwatchHeight = ( ScreenHeight ( ))/22
		SetWindowTitle ( "Comparing the 140 named colors" )
		HideToolbar ( )
		SetScreenSize ( SwatchWidth*NumberOfColumns, ( SwatchHeight + 4 )*NumberOfRows)
			
		ClearBackground ( White )
		Color ( Black )
		Rectangle (SwatchWidth*NumberOfColumns, ( SwatchHeight)*NumberOfRows, True) 
		
		// Give instructions
		SetFont ( ArialNarrow, 12, True, False, False)
		JumpTo ( 0, ScreenHeight ( ) - SwatchHeight - 30 )
		PrintLine ( " To compare two colors, drag the color samples side by side.  To enlarge, right click." )
		JumpTo ( 0, ScreenHeight ( ) - SwatchHeight - 7 )		
		Color ( White )
		Rectangle ( ScreenWidth ( ), 50 , True )
		JumpTo (0, ScreenHeight ( ) - SwatchHeight - 7)
		Color ( Black )
		PrintLine ( " Watch this space for the RGB values of the color you select." )
		
		SetFont ( "Arial", 9, False, False, False )
		
		// Display the color swatches 
		For Column = 1 To NumberOfColumns
			For Row = 1 To NumberOfRows
				
				// Calculate the position of the next swatch
				x = SwatchWidth * ( Column - 1 )
				y = SwatchHeight * ( Row - 1 )
				
				// Calculate the color number of this swatch
				ColorNumber = ( Column - 1 )*NumberOfRows + Row
				
				// Draw a rectangle in the appropriate color
				JumpTo ( x, y )
				Color ( ColorNumber)
				Rectangle ( SwatchWidth, SwatchHeight, True )
				JumpTo ( x, y - 2)

				// Print the name and number on the rectangle
				Color ( Contrast (ColorNumber) )
				PrintLine ( ColorNumber )
				JumpTo ( x, y +  SwatchHeight/2 - 2)
				PrintLine ( ColorName [ColorNumber] )
				JumpTo ( x, y - 2)
				PrintLine ( ColorNumber )
				
			Next
			JumpTo ( SwatchWidth*Column, 0 )
		Next
		
		ColorNumber = 0
		
		
		// Watch the mouse actions.
		While True

			If Mouse.Event = "ButtonDown" Then
				// When a button is depressed, show the RGB values, select
				// the color swatch indicated and start dragging
				If LeftPressed Then
				
					If Not Dragging Then
						ColorNumber = WhichSwatch ( )
						If ColorNumber > 0 Then
							z = z + 1
							Swatch [ColorNumber].ZIndex = z

							// Calculate how far the mouse is from the top
							// left of the swatch.
							OffsetX = MouseX - Swatch [ColorNumber].Left
							OffsetY = MouseY - Swatch [ColorNumber].Top
					
							Dragging = True
						End If	
					End If
				
					If ColorNumber > 0 Then
						JumpTo ( 0, ScreenHeight ( ) - SwatchHeight - 7 )		
						Color ( White )
						Rectangle ( ScreenWidth ( ), 50 , True )
						Color ( Black )
						SetFont ( ArialNarrow, 12, True, False, False)
						PrintLine ( " " +  ColorNumber + " " + ColorName [ColorNumber] + " has an RGB value of (" + RGB [ColorNumber] + ")." )
					End If
				End If
			End If
			
			
			// Quit dragging when the user releases the mouse button.
			If MouseEvent = "ButtonUp" Then
				Dragging = False
			End If

			
			// If the right mouse button is clicked, enlarge
			// the color swatch and show the RGB values.
			If RightPressed Then
				ColorNumber = WhichSwatch ( )
				
				If ColorNumber > 0 Then
					z = z + 1
					Swatch [ColorNumber].ZIndex = z
					
					JumpTo ( 0, ScreenHeight ( ) - SwatchHeight - 7 )		
					Color ( White )
					Rectangle ( ScreenWidth ( ), 50 , True )
					Color ( Black )
					SetFont ( ArialNarrow, 12, True, False, False)
					PrintLine ( " " +  ColorNumber + " " + ColorName [ColorNumber] + " has an RGB value of (" + RGB [ColorNumber] + ")." )

					Swatch [ColorNumber].ScaleX = 300
					Swatch [ColorNumber].ScaleY = 300
				End If
			End If
				
			
			// If we're dragging, move the active swatch to keep up 
			// with the mouse.
			If MouseEvent = "Move" And Dragging Then
				Swatch [ColorNumber].MoveTo ( MouseX - OffsetX, MouseY - OffsetY )
			End If
			
			RefreshScreen ( )
		
		End While
	
	End Method


	Function CaptureSwatch ( ) As Boolean
		Define Column As Integer = Mouse.X / SwatchWidth
		Define Row As Integer = Mouse.Y / SwatchHeight
		
		// If the mouse is on the bottom of the screen, do nothing.
		If Row > 19 Then
			Return False
		End If
		
		ColorNumber = Column * 20 + Row + 1
		
		// If this color is not yet an active sprite, capture it as a sprite.
		If Not Active [ColorNumber] Then
			BeginFrame ( )
			Active [ColorNumber] = True
			Swatch [ColorNumber] = CaptureSprite ( Column*SwatchWidth, Row*SwatchHeight, SwatchWidth, SwatchHeight  )
			z = z + 1
			Swatch [ColorNumber].ZIndex = z
			Swatch [ColorNumber].MoveTo ( Column*SwatchWidth + 5, Row*SwatchHeight + 5 )
			Swatch [ColorNumber].Show ( )
			Color ( White )
			JumpTo ( Column*SwatchWidth, Row*SwatchHeight )
			Rectangle ( SwatchWidth, SwatchHeight, True )
			Return True
		End If
		
		Return False
		
	End Function
	
	Method JumpTo ( x As Integer , y As Integer )
		DrawLine ( x, y, x, y )
	End Method
	
	Function Contrast ( Number As Integer ) As Integer
		// Selects a contrasting color (black or white) for each of the 140
		// named colors
		
		Define SelectedColors As Integer [] = { 8,10,12,22,26,28,29,32,35,36,41,43,45,51,52,57,81,83,91,96,98,99,113,116,117,120,122,125,126,129,131 }
		Define DarkColors As IntegerList 
		DarkColors.AddArray ( SelectedColors )
		If DarkColors.Contains ( Number ) Then
			Return White
		End If
		Return Black
		
	End Function

	Function WhichSwatch ( ) As Integer 
		// Returns the number of the active swatch, that is,
		// the number of the topmost swatch that contains the
		// point (MouseX, MouseY). 
		
		Define i As Integer 
		Define Possible As Boolean [140]          // Possibly active pentomino
		Define Selected As Integer = 0
		
		// First, find all the swatches that contain the point(MouseX, MouseY).
		For i = 1 To 140
			If Active [i] And Swatch [i].ContainsPoint ( MouseX, MouseY ) Then
				Possible [i] = True
				Selected = i
			Else
				Possible [i] = False
			End If
		Next
		
		// From all the possibilities, pick the one that is on top.
		For i = 1 To 140
			If Possible [i] Then
				If Swatch [i].ZIndex > Swatch [Selected].ZIndex Then
					Selected = i
				End If
			End If
		Next
		
		If selected = 0 Then
			CaptureSwatch ( )
		End If
		
		Return Selected
		
	End Function
	
End Program

